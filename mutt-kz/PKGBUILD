# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Loui Chang <louipc dot ist at NOSPAM gmail dot com>

pkgname=mutt-kz
pkgver=1.6.0.1
pkgrel=1
pkgdesc="Mutt with notmuch support and another improvements."
url="https://github.com/karelzak/mutt-kz"
arch=(i686 x86_64)
license=('GPL')
depends=('libidn' 'krb5' 'notmuch-runtime')
makedepends=('git' 'gnupg' 'libxslt')
conflicts=('mutt')
provides=('mutt')
options=('!strip')
source=("https://github.com/karelzak/$pkgname/archive/v$pkgver.tar.gz")
md5sums=('42a47d32888366b220b99f5183b5a487')
sha512sums=('a2ddb6b6805282596b8ee4235110f644fa5475a76343efedb3ee697267b08e43004673cd0c0c40e7126da3f629a7091d79f90a2c947b8f9d88ecd9480b1364dd')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ././prepare
  ./configure --enable-notmuch \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gpgme \
    --enable-pop \
    --enable-imap \
    --enable-smtp \
    --enable-pgp \
    --enable-hcache \
    --enable-sidebar \
    --with-curses=/usr \
    --with-regex \
    --with-gss=/usr \
    --with-ssl=/usr \
    --with-sasl \
    --with-idn

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "${pkgdir}"/usr/bin/{flea,muttbug}
  rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
  rm "${pkgdir}"/etc/mime.types{,.dist}
  install -Dm644 contrib/gpg.rc "${pkgdir}/etc/Muttrc.gpg.dist"
}
