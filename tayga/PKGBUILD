# Maintainer: Martin -nexus- Mlynář <nexus+arch@smoula.net>
pkgname=tayga
pkgver=0.9.2
pkgrel=3
pkgdesc="TAYGA NAT64 daemon"
arch=('i686' 'x86_64')
url="http://www.litech.org/tayga/"
license=('GPL')
backup=('etc/tayga.conf' 'etc/tayga.conf.example')
source=("http://www.litech.org/tayga/tayga-${pkgver}.tar.bz2"
        "tayga.conf"
        "tayga.service")
md5sums=('7a7b24165ce008df772f398d86fa280e'
         '0b818fcfe82dc618bb36da705de18ee3'
         '9d41c4a9effcf1402bae3141917effa9')
sha512sums=('a7a81cccb7c3e32f00f142b1bac598b732dd748772b307561239d695418ada5302182d487e51d1cf95ed736c0180c01db2f33409d0b9343d6587b76cbb3bc573'
            '38dea89dbc9e00b3259caa170e0c807bc6902ec96d85215abce23f4ae0cd90ea045455d7c04f738ecffc0105060d18fd772e12a0dd1eabfb1c069a8ec3a2e7b7'
            '434dc610c887f67efd6437c980d6a06c8f9f9533f9dbf12c722c1421d2753a81f18cd008c668a530d1b27ca24143b1a5d150a55163f7827e0903fac196675c22')

build() {
  cd $srcdir/tayga-${pkgver}
	./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/tayga-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../tayga.service "${pkgdir}"/usr/lib/systemd/system/tayga.service
  install -Dm644 ../tayga.conf "${pkgdir}"/etc/tayga.conf
  mkdir -p "${pkgdir}"/var/db/tayga
}
