# Maintainer: robertfoster

pkgname=ocaml-ffmpeg-git
pkgver=r323.1273a5e
pkgrel=1
pkgdesc="OCaml bindings to the FFmpeg library"
arch=('i686' 'x86_64')
url="https://github.com/savonet/ocaml-ffmpeg"
license=('LGPL2.1')
depends=('ocaml' 'ffmpeg')
makedepends=('git' 'dune>=2.0' 'dune-configurator' 'ocaml-findlib')
options=('!strip' '!makeflags')
source=("$pkgname::git+https://github.com/savonet/ocaml-ffmpeg")

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${pkgname}"
    dune build
}

package() {
    cd "${srcdir}/${pkgname}"

    export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
    mkdir -p "${OCAMLFIND_DESTDIR}"
    dune install \
        --destdir="$pkgdir" \
        --libdir="/usr/lib/ocaml"
}
md5sums=('SKIP')
