# Maintainer: Atte Lautanala <atte@lautana.la>
# Contributor: Trevor Mosey <trevor dot mosey at gmail dot com>

pkgname=flyctl
pkgver=0.3.234
pkgrel=1
pkgdesc="Command line tools for fly.io services"
arch=("x86_64")
url="https://github.com/superfly/flyctl"
license=("Apache-2.0")
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}::git+https://github.com/superfly/flyctl.git#tag=v${pkgver}")
b2sums=('4a11514767409db812335baed930ae9a2bc4a0b47cd5d73373d951f76ff8ccba3660619fb03eabbf6b338ad99cf0c3864334785f80da8ed718eed5e0b2eb3cff')

build() {
  BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%dT%H:%M:%SZ)"

  cd "$pkgname-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_ENABLED=0
  go build \
    -o bin/flyctl \
    -ldflags="-s -w -X github.com/superfly/flyctl/internal/buildinfo.buildDate=${BUILD_DATE} -X github.com/superfly/flyctl/internal/buildinfo.buildVersion=${pkgver} -X github.com/superfly/flyctl/internal/buildinfo.commit=$(git rev-parse --short --verify --quiet HEAD)" \
    -tags production \
    .
}

# disabled 2025-11-15 because the Rails testing requires a rake executable
#check() {
#  make -C "$pkgname-$pkgver" test
#}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "bin/flyctl" "$pkgdir/usr/bin/flyctl"
  ln -s "/usr/bin/flyctl" "$pkgdir/usr/bin/fly"
}
