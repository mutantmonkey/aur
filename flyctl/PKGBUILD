# Maintainer: Atte Lautanala <atte@lautana.la>
# Contributor: Trevor Mosey <trevor dot mosey at gmail dot com>

pkgname=flyctl
pkgver=0.3.228
pkgrel=1
pkgdesc="Command line tools for fly.io services"
arch=("x86_64")
url="https://github.com/superfly/flyctl"
license=("Apache-2.0")
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}::git+https://github.com/superfly/flyctl.git#tag=v${pkgver}")
b2sums=('78fc1682b3490a3c62a84015be1908be6dcb3b7a8f6920ebc2ef5ad549d40edcccf99e915765c3420fc1bc51e2fc74d52fbba0448082fa9a68f8da868bbaaaf9')

build() {
  BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%dT%H:%M:%SZ)"

  cd "$pkgname-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_ENABLED=0
  go build \
    -o bin/flyctl \
    -ldflags="-s -w -X github.com/superfly/flyctl/internal/buildinfo.buildDate=${BUILD_DATE} -X github.com/superfly/flyctl/internal/buildinfo.buildVersion=${pkgver} -X github.com/superfly/flyctl/internal/buildinfo.commit=$(git rev-parse --short --verify --quiet HEAD)" \
    -tags production \
    .
}

# disabled 2025-11-15 because the Rails testing requires a rake executable
#check() {
#  make -C "$pkgname-$pkgver" test
#}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "bin/flyctl" "$pkgdir/usr/bin/flyctl"
  ln -s "/usr/bin/flyctl" "$pkgdir/usr/bin/fly"
}
