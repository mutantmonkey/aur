# Maintainer: Atte Lautanala <atte@lautana.la>
# Contributor: Trevor Mosey <trevor dot mosey at gmail dot com>

pkgname=flyctl
pkgver=0.3.110
pkgrel=1
pkgdesc="Command line tools for fly.io services"
arch=("x86_64")
url="https://github.com/superfly/flyctl"
license=("Apache-2.0")
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}::git+https://github.com/superfly/flyctl.git#tag=v${pkgver}")
b2sums=('1b0a1ad64d91c6ebb0646a0d0ac4037099fc58bd89ae6f79202820803f50f3039a6ad2cedb9ec3131ebba95d62af92788f835f9f4bd292328052067bca9709d3')

build() {
  BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%dT%H:%M:%SZ)"

  cd "$pkgname-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_ENABLED=0
  go build \
    -o bin/flyctl \
    -ldflags="-s -w -X github.com/superfly/flyctl/internal/buildinfo.buildDate=${BUILD_DATE} -X github.com/superfly/flyctl/internal/buildinfo.buildVersion=${pkgver} -X github.com/superfly/flyctl/internal/buildinfo.commit=$(git rev-parse --short --verify --quiet HEAD)" \
    -tags production \
    .
}

check() {
  make -C "$pkgname-$pkgver" test
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "bin/flyctl" "$pkgdir/usr/bin/flyctl"
  ln -s "/usr/bin/flyctl" "$pkgdir/usr/bin/fly"
}
