# Contributor: David Birks <david@tellus.space>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Alda <alda@leetchee.fr>

pkgname=signal-beta
_pkgname=signal
pkgver=1.1.0beta.6
pkgrel=1
license=('GPL3')
pkgdesc='Signal Private Messenger for the Desktop'
depends=('electron' 'gconf')
makedepends=('npm' 'ruby-sass' 'python2' 'phantomjs' 'yarn' 'grunt-cli')
provides=('signal')
conflicts=('signal')
arch=("i686" "x86_64")
url='https://whispersystems.org'
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/WhisperSystems/Signal-Desktop/archive/v${pkgver/beta/-beta}.tar.gz"
        "signal.desktop"
        'electron_builder.patch'
        'electron_publisher.patch')
sha512sums=('795eb0dca300bafaf8b70ab4a59136a23387779051cf335fbaf870bb6f11edb589c98a6c91e018dfe61916cd3a553df0bf3f9164f3530cd92c1588718882ca25'
            'a264bfc7a4a7aac747daa588a2acbf1eddfd201bc795f0fbc18460a9b25f4460f364124e227a527fec22631cd84bc9e190f9f4978069e9c119eb556b9ff2d327'
            '3a5aaa8c0c7cb4c7de34474064d30de7f00bff6978fd1dff85c012811db64f8559a78d0cda764b233376358b8bbc4d142d24eae4adc412209ea0447e988a1bbb'
            'd920b745d8092da36d47e302ba9cb598365186a61eb01239c9227a47f66a0094e7f941159f89bc8ffc32cac09158e75172f2ccd4be96fc734ef57dcfa90c8b7d')

prepare() {
  cd "Signal-Desktop-${pkgver/beta/-beta}"
  sed -i 's/icon-gen \&\& grunt/icon-gen \&\& grunt -f/' package.json
  # workaround for https://github.com/WhisperSystems/Signal-Desktop/issues/1829
  patch -N -i ../electron_builder.patch
  patch -N -i ../electron_publisher.patch
}

build() {
  cd "Signal-Desktop-${pkgver/beta/-beta}"
  yarn install
  yarn pack-prod
}

package() {
  cd "Signal-Desktop-${pkgver/beta/-beta}/dist/linux-unpacked"

  install -Ddm755 "${pkgdir}/usr/lib/${_pkgname}"
  install -Ddm755 "${pkgdir}/usr/bin"

  cp -r * "${pkgdir}/usr/lib/${_pkgname}/"

  install -dm755 "${pkgdir}"/usr/share/icons/hicolor/256x256/apps
  cp ../../build/icons/png/256x256.png \
      "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/${_pkgname}.png

  install -Dm644 "${srcdir}"/${_pkgname}.desktop \
      "${pkgdir}"/usr/share/applications/${_pkgname}.desktop

  ln -s /usr/lib/${_pkgname}/signal-desktop "${pkgdir}/usr/bin/signal-desktop"
}
